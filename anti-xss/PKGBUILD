#
# system-linux.com rebuild alpha - djustice@system-linux.com
#

pkgname=anti-xss
pkgver=.2725dc9
pkgrel=1
pkgdesc='A XSS vulnerability scanner.'
groups=('system' 'system-webapp' 'system-scanner')
arch=('any')
url='https://github.com/lewangbtcc/anti-XSS'
license=('MIT')
depends=('python2' 'python2-reportlab' 'python2-tornado')
makedepends=('git')
source=("$pkgname::git+https://github.com/lewangbtcc/anti-XSS")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md doc/*
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm -rf README.md LICENSE doc

  cp -a * "$pkgdir/usr/share/$pkgname"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python2 anti-XSS.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

