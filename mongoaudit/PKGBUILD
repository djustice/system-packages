#
# system-linux.com rebuild alpha - djustice@system-linux.com
#

pkgname=mongoaudit
pkgver=222.70b83e8
pkgrel=6
pkgdesc='A powerful MongoDB auditing and pentesting tool .'
groups=('system' 'system-scanner' 'system-fuzzer')
arch=('any')
url='https://github.com/stampery/mongoaudit'
license=('MIT')
depends=('python' 'python-pymongo' 'python-urwid' 'python-setuptools'
         'python-dnspython')
makedepends=('git' 'python-setuptools')
source=("git+https://github.com/stampery/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd $pkgname

  python setup.py build
}

package() {
  cd $pkgname

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.*
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

