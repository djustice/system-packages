#
# system-linux.com rebuild alpha - djustice@system-linux.com
#

pkgname=dnstwist
pkgver=626.2562e2f
pkgrel=1
groups=('system' 'system-scanner' 'system-recon')
pkgdesc='Domain name permutation engine for detecting typo squatting, phishing and corporate espionage.'
arch=('any')
url='https://github.com/elceef/dnstwist'
license=('custom:unknown')
depends=('python' 'python-geoip' 'python-dnspython' 'python-requests'
         'python-ppdeep' 'python-python-whois' 'python-tld')
makedepends=('git' 'python-setuptools')
source=("git+https://github.com/elceef/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd $pkgname

  python setup.py build
}

package() {
  cd $pkgname

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
}

