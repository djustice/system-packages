#
# system-linux.com rebuild alpha - djustice@system-linux.com
#

pkgname=gowitness
pkgver=299.6b10eae
pkgrel=1
pkgdesc='A golang, web screenshot utility using Chrome Headless.'
arch=('x86_64' 'aarch64')
groups=('system' 'system-webapp' 'system-recon')
url='https://github.com/sensepost/gowitness'
license=('GPL3')
depends=('chromium')
makedepends=('git' 'go')
source=("git+https://github.com/sensepost/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd $pkgname

  GOPATH="$srcdir" go mod download
  GOPATH="$srcdir" go build \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-s -w" \
    -o $pkgname .
}

package() {
  cd $pkgname

  install -Dm 755 $pkgname "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
}

