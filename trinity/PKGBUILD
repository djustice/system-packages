#
# system-linux.com rebuild alpha - djustice@system-linux.com
#

pkgname=trinity
pkgver=5193.1374ba66
pkgrel=1
pkgdesc='A Linux System call fuzzer.'
groups=('system' 'system-fuzzer')
arch=('x86_64' 'aarch64')
url='http://codemonkey.org.uk/projects/trinity/'
license=('GPL')
makedepends=('git')
source=("git+https://github.com/kernelslacker/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd $pkgname

  ./configure

  make
}

package() {
  cd $pkgname

  install -Dm 644 README "$pkgdir/usr/share/doc/$pkgname/README"

  cd Documentation

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname" HACKING.txt TODO \
    Known-bugs.txt pro-tips.txt

  cd ..

  install -Dm 644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"

  make DESTDIR="$pkgdir/usr" install
}

