#
# system-linux.com rebuild alpha - djustice@system-linux.com
#

pkgname=ssrfmap
pkgver=104.f688ec9
pkgrel=2
pkgdesc='Automatic SSRF fuzzer and exploitation tool.'
groups=('system' 'system-webapp' 'system-scanner' 'system-fuzzer'
        'system-exploitation')
arch=('any')
url='https://github.com/swisskyrepo/SSRFmap'
license=('MIT')
depends=('python' 'python-flask' 'python-requests')
makedepends=('git')
source=("$pkgname::git+https://github.com/swisskyrepo/SSRFmap.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm README.md LICENSE

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python $pkgname.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

