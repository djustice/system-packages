#
# system-linux.com rebuild alpha - djustice@system-linux.com
#

pkgname=rabid
pkgver=v0.1.0.r112.gd794e0c
pkgrel=1
epoch=1
groups=('system' 'system-webapp' 'system-misc')
pkgdesc='A CLI tool and library allowing to simply decode all kind of BigIP cookies'
arch=('any')
url='https://noraj.github.io/rabid/'
license=('MIT')
depends=('ruby' 'ruby-paint' 'ruby-docopt')
makedepends=('git')
options=(!emptydirs)
source=("git+https://github.com/noraj/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  git describe --long | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd $pkgname

  gem build bigipcookie.gemspec
}

package() {
  cd $pkgname

  _gemdir="$(ruby -e'puts Gem.default_dir')"
  _release=$(gem build bigipcookie.gemspec | grep Version | cut -d' ' -f4)

  gem install --ignore-dependencies --no-user-install --no-document \
    -i "$pkgdir/$_gemdir" -n "$pkgdir/usr/bin" "$pkgname-$_release.gem"

  rm -f "$pkgdir/$_gemdir/cache/$pkgname-$_release.gem"
  find "$pkgdir/$_gemdir/extensions/" -name *.so -delete
  rm -rf "$pkgdir/$_gemdir/gems/$pkgname-$_release/test"

  install -Dm 644 "$pkgdir/$_gemdir/gems/$pkgname-$_release/LICENSE.txt" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

