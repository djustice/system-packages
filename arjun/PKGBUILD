#
# system-linux.com rebuild alpha - djustice@system-linux.com
#

pkgname=arjun
pkgver=234.16ee735
pkgrel=2
pkgdesc='HTTP parameter discovery suite.'
groups=('system' 'system-webapp' 'system-scanner')
arch=('any')
url='https://github.com/s0md3v/Arjun'
license=('GPL3')
depends=('python' 'python-requests' 'python-dicttoxml')
makedepends=('git' 'python-setuptools')
source=("$pkgname::git+https://github.com/s0md3v/Arjun.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd $pkgname

  python setup.py build
}

package() {
  cd $pkgname

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md CHANGELOG.md
}

