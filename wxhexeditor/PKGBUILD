#
# system-linux.com rebuild alpha - djustice@system-linux.com
#

pkgname=wxhexeditor
pkgver=733.f439d8f
pkgrel=1
pkgdesc='A free hex editor / disk editor for Linux, Windows and MacOSX.'
arch=('x86_64' 'aarch64')
groups=('system' 'system-binary' 'system-misc')
url='http://wxhexeditor.sourceforge.net/'
license=('GPL')
depends=('wxgtk3' 'mhash' 'python' 'python2')
makedepends=('git')
source=("$pkgname::git+https://github.com/EUA/wxHexEditor.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo "$(git rev-list --count HEAD)"."$(git rev-parse --short HEAD)"
}

build() {
  cd $pkgname

  make CXXFLAGS="${CXXFLAGS/-fno-rtti/}"
}

package() {
  cd $pkgname

  make DESTDIR="$pkgdir" PREFIX="/usr" install

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" docs/*
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

