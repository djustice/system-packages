#
# system-linux.com rebuild alpha - djustice@system-linux.com
#

pkgname=dnsvalidator
pkgver=82.146c9b0
pkgrel=3
pkgdesc='Maintains a list of IPv4 DNS servers by verifying them against baseline servers, and ensuring accurate responses.'
arch=('any')
groups=('system' 'system-networking' 'system-misc')
url='https://github.com/vortexau/dnsvalidator'
license=('custom:unknown')
depends=('python' 'python-requests' 'python-dnspython' 'python-colorclass'
         'python-netaddr')
makedepends=('git' 'python-setuptools' 'python-wheel' 'python-pip')
source=("git+https://github.com/vortexau/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd $pkgname

  python setup.py build
}

package() {
  cd $pkgname

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

