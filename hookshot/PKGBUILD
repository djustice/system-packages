#
# system-linux.com rebuild alpha - djustice@system-linux.com
#

pkgname=hookshot
pkgver=199.3258c3e
pkgrel=3
pkgdesc='Integrated web scraper and email account data breach comparison tool.'
groups=('system' 'system-webapp' 'system-scanner' 'system-recon'
        'system-social')
arch=('any')
url='https://github.com/andrew-vii/hookshot/'
license=('custom:unknown')
depends=('python' 'python-requests')
makedepends=('git')
source=("git+https://github.com/andrew-vii/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md

  rm README.md

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python main.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

