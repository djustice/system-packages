#
# system-linux.com rebuild alpha - djustice@system-linux.com
#

pkgname=sherlock
pkgver=2275.0ecb496
pkgrel=1
pkgdesc='Find usernames across social networks.'
groups=('system' 'system-social' 'system-recon')
arch=('any')
url='https://github.com/sherlock-project/sherlock'
license=('MIT')
depends=('python' 'python-beautifulsoup4' 'python-certifi' 'python-chardet'
         'python-colorama' 'python-idna' 'python-lxml' 'python-pysocks'
         'python-requests' 'python-requests-futures' 'python-soupsieve'
         'python-stem' 'python-torrequest' 'python-urllib3' 'python-pandas')
makedepends=('git')
source=("git+https://github.com/sherlock-project/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" *.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm README.md LICENSE CONTRIBUTING.md CODE_OF_CONDUCT.md requirements.txt

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname/$pkgname
exec python $pkgname.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

