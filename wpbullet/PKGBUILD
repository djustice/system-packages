#
# system-linux.com rebuild alpha - djustice@system-linux.com
#

pkgname=wpbullet
pkgver=34.6185112
pkgrel=5
pkgdesc='A static code analysis for WordPress (and PHP).'
groups=('system' 'system-code-audit' 'system-webapp')
arch=('any')
url='https://github.com/webarx-security/wpbullet'
license=('GPL2')
depends=('python' 'python-certifi' 'python-chardet' 'python-colorama'
         'python-idna' 'python-requests' 'python-tabulate' 'python-argparse'
         'python-terminaltables' 'python-urllib3')
makedepends=('git')
source=("git+https://github.com/webarx-security/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm README.md LICENSE

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python $pkgname.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

