#
# system-linux.com rebuild alpha - djustice@system-linux.com
#

pkgname=spyse
pkgver=47.cd11ba9
pkgrel=6
pkgdesc='Python API wrapper and command-line client for the tools hosted on spyse.com.'
groups=('system' 'system-recon')
arch=('any')
url='https://github.com/zeropwn/spyse.py'
license=('MIT')
depends=('python' 'python-argparse' 'python-requests')
makedepends=('git' 'python-setuptools' 'python-pip' 'python-wheel')
source=("$pkgname::git+https://github.com/zeropwn/spyse.py.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd $pkgname

  python setup.py build
}

package() {
  cd $pkgname

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

