#
# system-linux.com rebuild alpha - djustice@system-linux.com
#

pkgname=dns-reverse-proxy
pkgver=39.bd33162
pkgrel=1
groups=('system' 'system-proxy' 'system-networking')
pkgdesc='A reverse DNS proxy written in Go.'
arch=('x86_64' 'aarch64')
url='https://github.com/StalkR/dns-reverse-proxy'
license=('Apache')
makedepends=('git' 'go')
source=("git+https://github.com/StalkR/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd $pkgname

  GOPATH="$srcdir" go mod download
  GOPATH="$srcdir" go build \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-s -w" \
    -o $pkgname .
}


package() {
  cd $pkgname

  install -Dm 755 $pkgname "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

